buildscript {
    ext.kotlin_version = "1.4.31"
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.1.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath("org.jetbrains.dokka:dokka-gradle-plugin:0.10.1")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
    }
}

ext {
    compileSdkVersion = 30
    minSdkVersion = 14
    targetSdkVersion = 30

    probaSdkVersion = "0.1.2"
}

subprojects {
    version = rootProject.ext.probaSdkVersion
    group = 'ai.proba.probasdk'

    project.plugins.whenPluginAdded { plugin ->
        def isAndroidLibrary = "com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)
        if (isAndroidLibrary){
            project.android {
                compileSdkVersion rootProject.ext.compileSdkVersion
                defaultConfig.minSdkVersion rootProject.ext.minSdkVersion
            }
        }
    }
}
task clean(type: Delete) {
    delete rootProject.buildDir
}
