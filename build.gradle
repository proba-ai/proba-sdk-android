buildscript {
    ext.kotlin_version = "1.3.72"
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.0.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath("org.jetbrains.dokka:dokka-gradle-plugin:0.10.1")
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
    }
}

ext {
    compileSdkVersion = 29
    minSdkVersion = 14
    targetSdkVersion = 29

    appboosterSdkVersion = "0.2.0"

    mavenRepoUrl = (project.hasProperty('mavenRepoUrl')
            ? project.property('mavenRepoUrl') : '/tmp/appboosterRepo/')
}

subprojects {
    version = rootProject.ext.appboosterSdkVersion
    group = 'com.appbooster.appboostersdk'

    project.plugins.whenPluginAdded { plugin ->
        def isAndroidLibrary = "com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)
        if (isAndroidLibrary){
            project.android {
                compileSdkVersion rootProject.ext.compileSdkVersion
                defaultConfig.minSdkVersion rootProject.ext.minSdkVersion
            }
        }
    }
}
task clean(type: Delete) {
    delete rootProject.buildDir
}
